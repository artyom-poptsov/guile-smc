#!@GUILE@ \
-e main
!#

(use-modules (ice-9 getopt-long)
             (ice-9 pretty-print)
             (ice-9 eval-string)
             (smc core log)
             (smc fsm)
             (smc puml)
             (smc compiler))

(define (print-help-and-exit)
  (display "\
Usage: smc [options]

The program reads a PlantUML transition diagram from the standard input.

Options:
  --help, -h        Print this message and exit.
  --print-transition-table, -p
                    Print the FSM transition table.
  --load-path, -L   Add a paths separated by a colon to load paths.
  --modules, -U     Load additional modules.
  --fsm-name, -n    Set the name for the output FSM.
  --fsm-module, -m  Set the module for the output FSM.
  --validate        Validate the output FSM and print the validation result.
  --debug           Enable the debug mode.
")
  (exit 0))

(define %option-spec
  '((help                     (single-char #\h) (value #f))
    (print-transition-table   (single-char #\p) (value #f))
    (load-path                (single-char #\L) (value #t))
    (modules                  (single-char #\U) (value #t))
    (fsm-name                 (single-char #\n) (value #t))
    (fsm-module               (single-char #\m) (value #t))
    (validate                                   (value #f))
    (debug                                      (value #f))))

(define (main args)
  (let* ((options (getopt-long args %option-spec))
         (extra-load-paths (option-ref options 'load-path ""))
         (debug-mode?      (option-ref options 'debug     #f)))

    (when (option-ref options 'help #f)
      (print-help-and-exit))

    (for-each (lambda (dir) (add-to-load-path dir))
              (string-split extra-load-paths #\:))

    (log-use-stderr! debug-mode?)
    (let* ((modules      (option-ref options 'modules #f))
           (core-modules (list (resolve-module '(smc guards char))
                               (resolve-module '(smc puml))
                               (resolve-module '(smc fsm))))
           (fsm     (puml->fsm (current-input-port)
                               #:module (if modules
                                            (append core-modules
                                                    (map resolve-module
                                                         (eval-string
                                                          (string-append "(quote " modules ")"))))
                                            core-modules)
                               #:debug-mode? debug-mode?)))
      (when (option-ref options 'validate #f)
        (let ((validation-result (fsm-validate fsm)))
          (unless (null? validation-result)
            (pretty-print validation-result (current-error-port))
            (exit 1))))
      (cond
       ((option-ref options 'print-transition-table #f)
        (pretty-print
         (hash-table->transition-list (fsm-transition-table fsm))
         #:display? #t))
       (else
        (let ((name   (option-ref options 'fsm-name 'custom-fsm))
              (module (option-ref options 'fsm-module #f)))
          (fsm-compile fsm
                       #:fsm-name      name
                       #:fsm-module    (and module
                                            (eval-string (string-append "(quote " module ")")))
                       #:extra-modules (and modules
                                            (eval-string
                                             (string-append "(quote " modules ")"))))))))))

;;;

;; Local Variables:
;; mode: Scheme
;; End:

;;; smc.in ends here
